/*
Demo code accompanying the Chapter: "Realtime Deformation using Transform Feedback" by
Muhammad Mobeen Movania and Lin Feng. This is the shader code implementing the Verlet 
integration on the GPU. 

Author: Muhammad Mobeen Movania
Last Modified: 9 September 2011.
*/
#version 330
precision highp float;

#extension EXT_gpu_shader4 : require

layout( location = 0 )  in vec4 position_mass;
layout( location = 1 )  in vec4 prev_position;
 
uniform mat4 MVP;
uniform samplerBuffer tex_position_mass;
uniform samplerBuffer tex_prev_position_mass;
uniform vec2  inv_cloth_size;			//size of a single patch in world space
uniform vec2  step;						//delta texture size
uniform int texsize_x;					//size of position texture
uniform int texsize_y; 
uniform float dt, ksStr, ksShr, ksBnd, 
				 kdStr, kdShr, kdBnd, DEFAULT_DAMPING;
  
uniform vec3 gravity;
 
out vec4 out_position_mass;
out	vec4 out_prev_position;
 
// Resolve constraint in this space
const vec3 center = vec3(0,0,0);
const float radius = 1;
ivec2 getNextNeighbor(int n, out float ks, out float kd) { 
   //structural springs (adjacent neighbors)
   //        o
   //        |
   //     o--m--o
   //        |
   //        o
   if(n<4) {
       ks = ksStr;
       kd = kdStr;
   }
	if (n == 0)	return ivec2( 1,  0);
	if (n == 1)	return ivec2( 0, -1);
	if (n == 2)	return ivec2(-1,  0);
	if (n == 3)	return ivec2( 0,  1);
	
	//shear springs (diagonal neighbors)
	//     o  o  o
	//      \   /
	//     o  m  o
	//      /   \
	//     o  o  o
	if(n<8) {
       ks = ksShr;
       kd = kdShr;
   }
	if (n == 4) return ivec2( 1,  -1);
	if (n == 5) return ivec2( -1, -1);	
	if (n == 6) return ivec2(-1,  1);
	if (n == 7) return ivec2( 1,  1);
	
	//bend spring (adjacent neighbors 1 node away)
	//
	//o   o   o   o   o
	//        | 
	//o   o   |   o   o
	//        |   
	//o-------m-------o
	//        |  
	//o   o   |   o   o
	//        |
	//o   o   o   o   o 
	if(n<12) {
       ks = ksBnd;
       kd = kdBnd;
   }
	if (n == 8)	return ivec2( 2, 0);
	if (n == 9) return ivec2( 0, -2);
	if (n ==10) return ivec2(-2, 0);
	if (n ==11) return ivec2( 0, 2);
}

void sphereCollision(inout vec3 x, vec3 center, float r)
{
	vec3 delta = x - center;
	float dist = length(delta);
	if (dist < r) {
		x = center + delta*(r / dist);
	}
} 
 
 
void main(void) 
{  
	float m = position_mass.w;
	vec3 pos = position_mass.xyz;
    vec3 pos_old = prev_position.xyz;	
	vec3 vel = (pos - pos_old) / dt;
	float ks=0, kd=0;
	int index = gl_VertexID;
	int ix = index % texsize_x;
	int iy = index / texsize_x;
	if(index ==0 || index == (texsize_x-1))
		m = 0;
    vec3 F = gravity*m + (DEFAULT_DAMPING*vel);
	
	for(int k=0;k<12;k++) {
		ivec2 coord = getNextNeighbor(k, ks, kd);
		int j = coord.x;
		int i = coord.y;		
		if (((iy + i) < 0) || ((iy + i) > (texsize_y-1)))
			continue;

		if (((ix + j) < 0) || ((ix + j) > (texsize_x-1)))
			continue;

		int index_neigh = (iy + i) * texsize_x + ix + j;

		vec3 p2 = texelFetchBuffer(tex_position_mass, index_neigh).xyz;
		vec3 p2_last = texelFetchBuffer(tex_prev_position_mass, index_neigh).xyz;
		
		vec2 coord_neigh = vec2(ix + j, iy + i)*step;
		float rest_length = length(coord*inv_cloth_size);

		vec3 v2 = (p2- p2_last)/dt;
		vec3 deltaP = pos - p2;	
		vec3 deltaV = vel - v2;	 
		float dist = length(deltaP);
				
		float   leftTerm = -ks * (dist-rest_length);
		float  rightTerm = kd * (dot(deltaV, deltaP)/dist);		
		vec3 springForce = (leftTerm + rightTerm)* normalize(deltaP);
		F +=  springForce;	
	}
    vec3 acc = vec3(0);
	if(m!=0)
	   acc = F/m; 

	
	vec3 tmp = pos; 
	pos = pos * 2.0 - pos_old + acc* dt * dt;
	pos_old = tmp;

	//collision with floor
	if(pos.y<0) {
		pos.y=0;
	}
	out_position_mass = vec4(pos, m);	
	out_prev_position = vec4(pos_old,m);			  
	gl_Position = MVP*vec4(pos, 1);
	
	/*
	//Debug
	out_position_mass = vec4(ix,iy,0,1);
	out_prev_position = out_position_mass ;
	gl_Position = MVP*vec4(pos,1.0);
	*/
}